
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Сценарий = Параметры.Сценарий;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//Вставить содержимое обработчика
КонецПроцедуры


&НаКлиенте
Процедура Преобразовать(Команда)
	ПреобразоватьНаСервере();    
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаТаблица;
КонецПроцедуры

&НаСервере
Процедура ПреобразоватьНаСервере()
	
	МассивКлючей = СтрРазделить(Текст,Символы.ПС,Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сравнение.Сценарий КАК Сценарий,
	|	Сравнение.Ключ КАК Ключ,
	|	Сравнение.МодульБазовойКонфигурации.ОбъектКонфигурации.Конфигурация КАК Конфигурация,
	|	Сравнение.МодульБазовойКонфигурации.ОбъектКонфигурации КАК ОбъектКонфигурации,
	|	Сравнение.МодульБазовойКонфигурации КАК СвойствоОбъектаКонфигурации
	|ПОМЕСТИТЬ ВтКлючи
	|ИЗ
	|	РегистрСведений.СравнениеКонфигураций КАК Сравнение
	|ГДЕ
	|	Сравнение.Сценарий = &Сценарий
	|	И Сравнение.Ключ В(&МассивКлючей)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтКлючи.Сценарий КАК Сценарий,
	|	ВтКлючи.Ключ КАК Ключ,
	|	ВтКлючи.Конфигурация КАК Конфигурация,
	|	ВтКлючи.ОбъектКонфигурации КАК ОбъектКонфигурации,
	|	ВтКлючи.СвойствоОбъектаКонфигурации КАК СвойствоОбъектаКонфигурации,
	|	ЕСТЬNULL(ОбработанныеОбъектыКонфигурации.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОбработки.ТребуетсяОбработка)) КАК Статус,
	|	ЕСТЬNULL(ОбработанныеОбъектыКонфигурации.Ответственный, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Ответственный,
	|	ЕСТЬNULL(ОбработанныеОбъектыКонфигурации.Комментарий, """") КАК Комментарий,
	|	ЕСТЬNULL(ОбработанныеОбъектыКонфигурации.Дата, НЕОПРЕДЕЛЕНО) КАК Дата,
	|	ЕСТЬNULL(ОбработанныеОбъектыКонфигурации.Приоритет, ЗНАЧЕНИЕ(Перечисление.Приоритеты.Основной)) КАК Приоритет
	|ИЗ
	|	ВтКлючи КАК ВтКлючи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбработанныеОбъектыКонфигурации КАК ОбработанныеОбъектыКонфигурации
	|		ПО ВтКлючи.Сценарий = ОбработанныеОбъектыКонфигурации.Сценарий
	|			И ВтКлючи.Конфигурация = ОбработанныеОбъектыКонфигурации.Конфигурация
	|			И ВтКлючи.ОбъектКонфигурации = ОбработанныеОбъектыКонфигурации.ОбъектКонфигурации
	|			И ВтКлючи.СвойствоОбъектаКонфигурации = ОбработанныеОбъектыКонфигурации.СвойствоОбъектаКонфигурации";
	Запрос.УстановитьПараметр("МассивКлючей",МассивКлючей);
	Запрос.УстановитьПараметр("Сценарий",Сценарий);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТаблицаПланируемыхЗадач.Очистить();
	
	Пока Выборка.Следующий() Цикл      
		стр_н = ТаблицаПланируемыхЗадач.Добавить();
		ЗаполнитьЗначенияСвойств(стр_н,Выборка);  
		
		// проставим Сценарий если не заполнено
		Если НЕ ЗначениеЗаполнено(стр_н.Сценарий) Тогда
			стр_н.Сценарий = Сценарий;
		КонецЕсли;		
		
		// проставим статус если не заполнено
		Если НЕ ЗначениеЗаполнено(стр_н.Статус) Тогда
			стр_н.Статус = Перечисления.СтатусыОбработки.ТребуетсяОбработка;
		КонецЕсли;

		// проставим статус если не заполнено
		Если НЕ ЗначениеЗаполнено(стр_н.Приоритет) Тогда
			стр_н.Приоритет = Перечисления.Приоритеты.Основной;
		КонецЕсли;

		// проставим статус если не заполнено
		Если НЕ ЗначениеЗаполнено(стр_н.Дата) Тогда
			стр_н.Дата = ТекущаяДата();
		КонецЕсли;
		
		стр_н.Ответственный = Ответственный;
		
	КонецЦикла;	
	
КонецПроцедуры


&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере();
КонецПроцедуры


&НаСервере
Процедура СохранитьНаСервере()              
	
	Для каждого стр из ТаблицаПланируемыхЗадач Цикл
		
		МенеджерЗаписи = РегистрыСведений.ОбработанныеОбъектыКонфигурации.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи,стр);
		МенеджерЗаписи.Записать(Истина);
		
	КонецЦикла;

КонецПроцедуры

